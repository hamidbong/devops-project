def SERVICE_NAME = 'service-python'  // adapter au nom de ton serviceed

pipeline {
    agent any

    environment {
        AUTH_TOKEN_SONAR = credentials('SONAR_AUTH_TOKEN')
        IMAGE_NAME  = "hamidbong/${SERVICE_NAME}"
        TRIVY_CACHE = 'trivy_cache'
        PYTHONPATH  = "${WORKSPACE}/${SERVICE_NAME}"  // Full absolute path
        PYTHON_VERSION = '3.12.3'
    }

    stages {

        stage('Checkout') {
            steps {
                checkout scm
            }
        }

        stage('Run Tests') {
            steps {
                dir("${SERVICE_NAME}") {
                    sh '''
                        python3 -m venv venv
                        . venv/bin/activate
                        pip install --upgrade pip
                        pip install -r requirements.txt
                        pip install pytest pytest-cov

                        pytest --cov=./ --cov-report=xml:coverage.xml --junitxml=test-results.xml -v
                    '''
                }
                archiveArtifacts artifacts: "${SERVICE_NAME}/test-results.xml,${SERVICE_NAME}/coverage.xml", allowEmptyArchive: true
            }
        }


        stage('OWASP Dependency-Check') {
            steps {
                dir("${SERVICE_NAME}") {
                    withCredentials([string(credentialsId: 'nvd-api-key', variable: 'NVD_API_KEY')]) {
                        dependencyCheck additionalArguments: '''
                            -s . 
                            -o "./dependency-check-report" 
                            -f ALL 
                            --prettyPrint 
                            --nvdApiKey=${NVD_API_KEY}''',
                            odcInstallation: 'dependency-check-12.1.9'
                    }

                    dependencyCheckPublisher pattern: 'dependency-check-report/dependency-check-report.xml',
                        failedTotalCritical: 1,
                        unstableTotalHigh: 1

                    archiveArtifacts artifacts: 'dependency-check-report/**', allowEmptyArchive: true
                }
            }
        }

        stage('V√©rification vuln√©rabilit√©s') {
            steps {
                script {
                    def report = readFile("${SERVICE_NAME}/dependency-check-report/dependency-check-report.xml")
                    if (report.contains('<baseSeverity>CRITICAL</baseSeverity>') || report.contains('<severity>critical</severity>')) {
                        error("Vuln√©rabilit√©s CRITIQUES d√©tect√©es ‚Äî arr√™t du pipeline.")
                    }
                }
            }
        }

        stage('SonarQube Analysis') {
            steps {
                withSonarQubeEnv('Sonarqube') {
                    script {
                        sh """
                            ${tool('SonarScanner')}/bin/sonar-scanner \
                                -Dsonar.projectKey=${SERVICE_NAME} \
                                -Dsonar.projectName=${SERVICE_NAME} \
                                -Dsonar.sources=${SERVICE_NAME} \
                                -Dsonar.python.version=${PYTHON_VERSION} \
                                -Dsonar.python.coverage.reportPaths=coverage.xml \
                                -Dsonar.exclusions=**/venv/**,**/__pycache__/** \
                                -Dsonar.sourceEncoding=UTF-8 \
                                -Dsonar.projectVersion=${env.BUILD_NUMBER}
                        """
                    }
                }
            }
        }

        stage("Quality Gates") {
            steps {
                retry(5) {
                    timeout(time: 1, unit: 'MINUTES') {
                        waitForQualityGate abortPipeline: true
                    }
                    sleep(time: 5, unit: 'SECONDS')
                }
            }
        }

        stage('Check Docker Daemon') {
            steps {
                sh 'docker version'
            }
        }

        stage('Build Docker Image') {
            steps {
                dir("${SERVICE_NAME}") {
                    script {
                        dockerImage = docker.build("${IMAGE_NAME}:${env.BUILD_ID}")
                    }
                }
            }
        }

        stage('Setup Trivy Caches') {
            steps {
                sh 'docker volume create --name trivy_cache || true'
                sh 'docker volume create --name trivy_javadb_cache || true'
            }
        }

        stage('Scan Docker Image with Trivy') {
            steps {
                sh '''
                    docker run --rm \
                        -v /var/run/docker.sock:/var/run/docker.sock \
                        -v trivy_cache:/root/.cache/trivy \
                        -v $(pwd):/project \
                        aquasec/trivy:latest \
                        image ${IMAGE_NAME}:${BUILD_ID} \
                        --exit-code 1 \
                        --severity CRITICAL,HIGH \
                        --ignore-unfixed \
                        --format json \
                        --output /project/trivy-report.json
                '''
                archiveArtifacts artifacts: 'trivy-report.json', allowEmptyArchive: true
            }
        }

        stage('Push Docker Image') {
            steps {
                dir("${SERVICE_NAME}") {
                    script {
                        docker.withRegistry('https://index.docker.io/v1/', 'dockerHub-login') {
                            dockerImage.push()
                            dockerImage.push('latest')
                        }
                    }
                }
            }
        }

        stage('Cleanup Docker') {
            steps {
                sh "docker rmi ${IMAGE_NAME}:${env.BUILD_ID} || true"
                sh "docker rmi ${IMAGE_NAME}:latest || true"
                sh "docker image prune -f || true"
            }
        }

        stage('Deploy to Kubernetes') {
            steps {
                withKubeConfig([credentialsId: 'kubeconfig']) {
                    sh """
                        cat k8s/${SERVICE_NAME}/deployment.yaml | \
                        sed "s/{{BUILD_NUMBER}}/${env.BUILD_ID}/g" | kubectl apply -f -
                        kubectl apply -f k8s/${SERVICE_NAME}/service.yaml
                    """
                }
            }
        }

        stage('Verify Deployment') {
            steps {
                withKubeConfig([credentialsId: 'kubeconfig']) {
                    sh "kubectl rollout status deployment/${SERVICE_NAME}-deployment --timeout=300s"
                    sh "kubectl wait --for=condition=ready pod -l app=${SERVICE_NAME} --timeout=300s"
                }
            }
        }

        stage('Git Tag') {
            when {
                branch 'main'
            }
            steps {
                script {
                    def versionTag = "v${env.BUILD_NUMBER}"
                    sh 'git config user.name "Jenkins CI"'
                    sh 'git config user.email "jenkins@example.com"'
                    sh "git tag ${versionTag}"
                    sh "git push origin ${versionTag}"
                }
            }
        }
    }

    post {
        always {
            archiveArtifacts artifacts: '**/coverage.xml,**/test-results.xml', allowEmptyArchive: true
            echo "üì¶ Build #${env.BUILD_NUMBER} completed"
        }
        success {
            echo '‚úÖ Pipeline completed successfully'
        }
        failure {
            echo '‚ùå Pipeline failed'
        }
    }
}
