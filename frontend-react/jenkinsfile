def SERVICE_NAME = 'frontend-react'  // adapter √† chaque fichier

pipeline {
    agent any

    tools {
        nodejs 'Node18'
    }

    environment {
        SONARQUBE     = 'sonarqube_devops_master_analyse'
        NVD_API_KEY   = credentials('nvd-api-key')
        IMAGE_NAME    = "hamidbong/frontend-react"
    }

    stages {

        stage('Check versions') {
            steps {
                sh 'node -v'
                sh 'npm -v'
            }
        }

        stage('Checkout') {
            steps {
                checkout scm
            }
        }

        stage('Install & Verify Lock') {
            steps {
                dir("${SERVICE_NAME}") {
                    sh 'npm ci --no-audit'
                    sh 'npm install --package-lock-only --no-audit'
                    sh 'git diff --exit-code package-lock.json || { echo "package-lock.json out of date! Commit the changes."; exit 1; }'
                }
            }
        }

        stage('Run Tests') {
            steps {
                dir("${SERVICE_NAME}") {
                    sh 'npm test -- --coverage'
                }
            }   
        }

        stage('OWASP Dependency-Check') {
            steps {
                dir("${SERVICE_NAME}") {
                    withCredentials([string(credentialsId: 'nvd-api-key', variable: 'NVD_API_KEY')]) {
                        dependencyCheck additionalArguments: '''
                            -s . 
                            -o "./dependency-check-report" 
                            -f ALL 
                            --prettyPrint
                            --nvdApiKey=${NVD_API_KEY}''',
                            odcInstallation: 'DependencyCheck'
                    }
                    archiveArtifacts artifacts: 'dependency-check-report/**', allowEmptyArchive: true
                }
            }
        }

        stage('V√©rification vuln√©rabilit√©s') {
            steps {
                script {
                    def report = readFile("${SERVICE_NAME}/dependency-check-report/dependency-check-report.xml")
                    if (report.contains('<baseSeverity>CRITICAL</baseSeverity>') || report.contains('<severity>critical</severity>')) {
                        error("Vuln√©rabilit√©s CRITIQUES d√©tect√©es ‚Äî arr√™t du pipeline.")
                    }
                }
            }
        }

        stage('SonarQube Analysis') {
            steps {
                withSonarQubeEnv("${SONARQUBE}") {
                    script {
                        def nodePath = sh(returnStdout: true, script: 'which node').trim()

                        sh """
                            ${tool('SonarScanner')}/bin/sonar-scanner \
                                -Dsonar.projectKey=${SERVICE_NAME} \
                                -Dsonar.projectName=${SERVICE_NAME} \
                                -Dsonar.projectVersion=${env.BUILD_NUMBER} \
                                -Dsonar.sources=${SERVICE_NAME} \
                                -Dsonar.nodejs.executable=${nodePath} \
                                -Dsonar.javascript.lcov.reportPaths=${SERVICE_NAME}/coverage/lcov.info
                        """
                    }
                }
            }
        }

        stage("Quality Gates") {
            steps {
                retry(5) {
                    timeout(time: 1, unit: 'MINUTES') {
                        waitForQualityGate abortPipeline: true
                    }
                    sleep(time: 10, unit: 'SECONDS')
                }
            }
        }

        stage('Trivy Filesystem Scan') {
            steps {
                dir("${SERVICE_NAME}") {
                    sh '''
                        docker run --rm \
                            -v $(pwd):/scan \
                            aquasec/trivy fs \
                            --security-checks vuln,config \
                            --output /project/trivy-Filesystem-report.json \
                            --severity CRITICAL \
                            /scan
                    '''
                }
            }
        }

        stage('Check Docker Daemon') {
            steps {
                sh 'docker version'
            }
        }

        stage('Build Docker Image') {
            steps {
                dir("${SERVICE_NAME}") {
                    timeout(time: 15, unit: 'MINUTES') {
                        script {
                            dockerImage = docker.build("${IMAGE_NAME}:${env.BUILD_ID}")
                        }
                    }
                }
            }
        }

        stage('Setup Trivy Caches') {
            steps {
                sh 'docker volume create --name trivy_cache || true'
                sh 'docker volume create --name trivy_javadb_cache || true'
            }
        }

        stage('Scan with Trivy') {
            steps {
                script {
                    try {
                        sh '''
                            docker run --rm \
                                -v /var/run/docker.sock:/var/run/docker.sock \
                                -v trivy_cache:/root/.cache/trivy \
                                -v trivy_javadb_cache:/root/.cache/trivy-java-db \
                                -e TRIVY_TIMEOUT=30m \
                                -v $(pwd):/project \
                                aquasec/trivy:latest \
                                image ${IMAGE_NAME}:${BUILD_ID} \
                                --severity CRITICAL,HIGH \
                                --ignore-unfixed \
                                --timeout 30m \
                                --format json \
                                --output /project/trivy-report.json
                        '''
                    } catch (err) {
                        error "Trivy scan failed due to CRITICAL/HIGH vulnerabilities"
                    }
                }
                archiveArtifacts artifacts: 'trivy-report.json', allowEmptyArchive: true
            }
        }

        stage('Push Docker Image') {
            steps {
                dir("${SERVICE_NAME}") {
                    script {
                        docker.withRegistry('https://index.docker.io/v1/', 'dockerHub-login') {
                            dockerImage.push()
                            dockerImage.push('latest')
                        }
                    }
                }
            }
        }
        
        stage('Cleanup Docker') {
            steps {
                script {
                    // Supprime l'image locale apr√®s le push
                    sh "docker rmi ${IMAGE_NAME}:${env.BUILD_ID} || true"
                    sh "docker rmi ${IMAGE_NAME}:latest || true"
                    
                    // Optionnel : Nettoyage des images "<none>" (dangling)
                    sh "docker image prune -f || true"
                }
            }
        }

        stage('Apply Kubernetes Files') {
            steps {
                withKubeConfig([credentialsId: 'kubeconfig']) {
                    sh """
                        cat k8s/${SERVICE_NAME}/deployment.yaml | \
                        sed "s/{{BUILD_NUMBER}}/${env.BUILD_ID}/g" | kubectl apply -f -
                        kubectl apply -f k8s/${SERVICE_NAME}/service.yaml
                        # kubectl apply -f k8s/${SERVICE_NAME}/ingress.yaml
                    """
                }
            }
        }

        stage('Verify Deployment') {
            steps {
                withKubeConfig([credentialsId: 'kubeconfig']) {
                    sh "kubectl rollout status deployment/${SERVICE_NAME}-deployment --timeout=300s"
                    sh "kubectl wait --for=condition=ready pod -l app=${SERVICE_NAME} --timeout=300s"
                }
            }
        }

        stage('Git Tag') {
            when {
                branch 'main'  // on ne tag que sur la branche main
            }
            steps {
                script {
                    def versionTag = "v${env.BUILD_NUMBER}"  // Exemple : v27

                    // Configure l'utilisateur Git si n√©cessaire (utile dans Jenkins agents)
                    sh 'git config user.name "Jenkins CI"'
                    sh 'git config user.email "jenkins@example.com"'

                    // Cr√©e le tag et le pousse sur le repo distant
                    sh "git tag ${versionTag}"
                    sh "git push origin ${versionTag}"

                    echo "‚úÖ Git tag ${versionTag} created and pushed."
                }
            }
        }
    }

    post {
        always {
            echo "üßæ Pipeline completed on build #${env.BUILD_NUMBER}"
            // Archive artifacts are now done within their respective stages
        }
        success {
            echo '‚úÖ Build succeeded!'
        }
        failure {
            echo '‚ùå Build failed!'
        }
    }
}